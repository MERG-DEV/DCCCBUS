divert(-1)

define(dcc_one_half_time, 58)
define(dcc_zero_half_time, 100)

define(input_dcc_one,
       -- DCC One
       set_dcc_on
       wait for u_sec(dcc_one_half_time);
       set_dcc_off
       wait for u_sec(dcc_one_half_time);)

define(input_dcc_zero,
       -- DCC Zero
       set_dcc_on
       wait for u_sec(dcc_zero_half_time);
       set_dcc_off
       wait for u_sec(dcc_zero_half_time);)

define(input_dcc_bit, {ifelse($1, 1, input_dcc_one, input_dcc_zero)})

define(input_dcc_bits,
       {ifelse($1, {}, {}, input_dcc_bit(substr($1, 0, 1))
                           {input_dcc_bits(substr($1, 1))})})

define(event_to_acc, {substr(eval($1 + 8, 2, 12), $2, $3)})

define(cmp_event_to_acc,
       {eval({0b}eval($2, 1) - {0b}event_to_acc($1, 0, $2), 2, $2)})

define(checksum, {eval({0b}$1 ^ {0b}$2, 2, 8)})

define(acc_byte_1, {{10}event_to_acc($1, 3, 6)})

define(basic_acc_byte_2,
       {{1}cmp_event_to_acc($1, 3)ifelse($2, Off, 0, 1)event_to_acc($1, 9, 3)})

define(_input_dcc_basic_acc,
       -- Preamble
       {input_dcc_bits($1)}
       -- Byte One
       input_dcc_zero
       {input_dcc_bits($2)}
       -- Byte Two
       input_dcc_zero
       {input_dcc_bits($3)}
      -- Checksum
       input_dcc_zero
       {input_dcc_bits($4)}
       -- End of Packet
       input_dcc_one)

define(input_dcc_basic_acc,
       {_input_dcc_basic_acc(eval(14, 1),
                             acc_byte_1($1),
                             basic_acc_byte_2($1, $2),
                             checksum(acc_byte_1($1),
                                      basic_acc_byte_2($1, $2)))})

define(input_dcc_basic_acc_pair,
       {input_dcc_basic_acc(eval(($1 << 1) ifelse($2, On, + 1)), On)})

define(input_dcc_basic_too_short_acc,
       -- Preamble
       {input_dcc_bits(eval(14, 1))}
       -- Byte One
       input_dcc_zero
       {input_dcc_bits(acc_byte_1(1))}
       -- Checksum
       input_dcc_zero
       {input_dcc_bits(acc_byte_1(1))}
       -- End of Packet
       input_dcc_one)

define(input_dcc_basic_too_long_acc,
       -- Preamble
       {input_dcc_bits(eval(14, 1))}
       -- Byte One
       input_dcc_zero
       {input_dcc_bits(acc_byte_1(1))}
       -- Byte Two
       input_dcc_zero
       {input_dcc_bits(basic_acc_byte_2(1, On))}
       -- Byte Three
       input_dcc_zero
       {input_dcc_bits(01010101)}
       -- Checksum
       input_dcc_zero
       {input_dcc_bits(checksum(acc_byte_1(1), checksum(basic_acc_byte_2(1, On),
                                                        01010101)))}
       -- End of Packet
       input_dcc_one)

define(input_dcc_basic_bad_checksum_acc,
       {_input_dcc_basic_acc(eval(14, 1),
                             acc_byte_1(1),
                             basic_acc_byte_2(1, On),
                             01010101)})

define(input_dcc_basic_short_preamble_acc,
       {_input_dcc_basic_acc(eval(9, 1),
                             acc_byte_1(1),
                             basic_acc_byte_2(1, On),
                             checksum(acc_byte_1(1),
                                      basic_acc_byte_2(1, On)))})

define(input_dcc_basic_invalid_byte_1_a_acc,
       {_input_dcc_basic_acc(eval(14, 1),
                             {00}event_to_acc(1, 3, 6),
                             basic_acc_byte_2(1, On),
                             checksum({00}event_to_acc(1, 3, 6),
                                      basic_acc_byte_2(1, On)))})

define(input_dcc_basic_invalid_byte_1_b_acc,
       {_input_dcc_basic_acc(eval(14, 1),
                             {01}event_to_acc(1, 3, 6),
                             basic_acc_byte_2(1, On),
                             checksum({01}event_to_acc(1, 3, 6),
                                      basic_acc_byte_2(1, On)))})

define(input_dcc_basic_invalid_byte_1_c_acc,
       {_input_dcc_basic_acc(eval(14, 1),
                             {11}event_to_acc(1, 3, 6),
                             basic_acc_byte_2(1, On),
                             checksum({11}event_to_acc(1, 3, 6),
                                      basic_acc_byte_2(1, On)))})

define(input_dcc_basic_invalid_byte_2_acc,
       {_input_dcc_basic_acc(eval(14, 1),
                             acc_byte_1(1),
                             {0}cmp_event_to_acc(1, 3){0}event_to_acc(1, 9, 3),
                             checksum({00}event_to_acc(1, 3, 6),
                                      {0}cmp_event_to_acc(1, 3){0}event_to_acc(1, 9, 3)))})

divert(0)dnl
