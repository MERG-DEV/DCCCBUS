divert(-1)

define(dcc_one_half_time, 58)
define(dcc_zero_half_time, 100)

define(input_dcc_one,
       -- DCC One
       set_dcc_on
       wait for u_sec(dcc_one_half_time);
       set_dcc_off
       wait for u_sec(dcc_one_half_time);)

define(input_dcc_zero,
       -- DCC Zero
       set_dcc_on
       wait for u_sec(dcc_zero_half_time);
       set_dcc_off
       wait for u_sec(dcc_zero_half_time);)

define(input_dcc_bit, {ifelse($1, 1, input_dcc_one, input_dcc_zero)})

define(input_dcc_bits,
       {ifelse($1, {}, {}, input_dcc_bit(substr($1, 0, 1))
                           {input_dcc_bits(substr($1, 1))})})

define(event_to_acc, {substr(eval($1 + 8, 2, 12), $2, $3)})

define(cmp_event_to_acc,
       {eval({0b}eval($2, 1) - {0b}event_to_acc($1, 0, $2), 2, $2)})

define(checksum, {eval({0b}$1 ^ {0b}$2, 2, 8)})

define(basic_acc_byte_1, {{10}event_to_acc($1, 3, 6)})

define(basic_acc_byte_2,
       {{1}cmp_event_to_acc($1, 3)ifelse($2, Off, 0, 1)event_to_acc($1, 9, 3)})

define(_input_dcc_basic_acc,
       -- Preamble
       {input_dcc_bits(eval($1, 1){0})}
       -- Byte One
       {input_dcc_bits(basic_acc_byte_1($2){0})}
       -- Byte Two
       {input_dcc_bits(basic_acc_byte_2($2, $3){0})}
      -- Checksum
       {input_dcc_bits(checksum(basic_acc_byte_1($2),
                                basic_acc_byte_2($2, $3)))}
       -- End of Packet
       input_dcc_one)

define(input_dcc_basic_acc, {_input_dcc_basic_acc(14, $1, $2)})

define(input_dcc_basic_acc_pair,
       {input_dcc_basic_acc(eval(($1 << 1) ifelse($2, On, + 1)), On)})

divert(0)dnl
